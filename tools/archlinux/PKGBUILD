# Maintainer: Lizzy Fleckenstein <lizzy@vlhl.dev>

pkgname=dragonblocks-git
pkgver=0.2.2.62.gcc44857
pkgrel=1
pkgdesc='A multiplayer voxelgame for POSIX systems.'
arch=('i686' 'x86_64')
url='https://github.com/dragonblocks/dragonblocks_alpha'
license=('GPLv3+')
depends=('sqlite3' 'freetype2' 'zlib' 'glfw' 'glew' 'mesa')
makedepends=('meson' 'gcc' 'lua' 'cmake')
source=("git+https://github.com/dragonblocks/dragonblocks_alpha.git")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/dragonblocks_alpha"
	git describe --tags | sed 's/-/./g'
}

prepare() {
	cd "$srcdir/dragonblocks_alpha"
	meson subprojects download
}

build() {
	cd "$srcdir/dragonblocks_alpha"
	meson setup build \
		-Dbuildtype=plain \
		-Doptimization=2 \
		-Dasset_path="/usr/share/dragonblocks/" \
		--prefix=/usr
	meson compile -C build
}

package() {
	cd "$srcdir/dragonblocks_alpha"
	meson install -C build --destdir "$pkgdir"

	install -m 755 "singleplayer.sh" "$pkgdir/usr/bin/dragonblocks-singleplayer"

	(cd assets &&
		find -type f -exec install -Dm 644 "{}" "$pkgdir/usr/share/dragonblocks/{}" \; )
}
